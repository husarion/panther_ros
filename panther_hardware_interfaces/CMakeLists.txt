cmake_minimum_required(VERSION 3.10.2)

# Handle superbuild first
option(USE_SUPERBUILD "Whether or not a superbuild should be invoked" ON)

if(USE_SUPERBUILD)
  project(SUPERBUILD NONE)
  include(cmake/SuperBuild.cmake)
  return()
else()
  project(panther_hardware_interfaces)
endif()

# Default to C++17
if(NOT CMAKE_CXX_STANDARD)
  set(CMAKE_CXX_STANDARD 17)
endif()

if(CMAKE_COMPILER_IS_GNUCXX OR CMAKE_CXX_COMPILER_ID MATCHES "Clang")
  add_compile_options(-Wall -Wextra -Wpedantic)
endif()

# Find dependencies
set(PACKAGE_INCLUDE_DEPENDS
    ament_cmake
    controller_interface
    diagnostic_updater
    generate_parameter_library
    geometry_msgs
    hardware_interface
    imu_filter_madgwick
    panther_msgs
    panther_utils
    phidgets_api
    PkgConfig
    pluginlib
    rclcpp
    rclcpp_lifecycle
    realtime_tools
    std_srvs
    tf2_geometry_msgs
    tf2_ros)

foreach(Dependency IN ITEMS ${PACKAGE_INCLUDE_DEPENDS})
  find_package(${Dependency} REQUIRED)
endforeach()

generate_parameter_library(phidgets_spatial_parameters
                           config/phidgets_spatial_parameters.yaml)

set(ENV{PKG_CONFIG_PATH}
    "${CMAKE_INSTALL_PREFIX}/lib/pkgconfig:$ENV{PKG_CONFIG_PATH}")

pkg_check_modules(LIBLELY_COAPP REQUIRED IMPORTED_TARGET liblely-coapp)
pkg_check_modules(LIBGPIOD REQUIRED IMPORTED_TARGET libgpiodcxx)

add_library(
  ${PROJECT_NAME} SHARED
  src/canopen_controller.cpp
  src/gpio_controller.cpp
  src/gpio_driver.cpp
  src/motors_controller.cpp
  src/panther_imu.cpp
  src/panther_system_e_stop.cpp
  src/panther_system_ros_interface.cpp
  src/panther_system.cpp
  src/roboteq_data_converters.cpp
  src/roboteq_driver.cpp
  src/roboteq_error_filter.cpp
  src/utils.cpp)

target_include_directories(${PROJECT_NAME} PRIVATE include)

ament_target_dependencies(${PROJECT_NAME} ${PACKAGE_INCLUDE_DEPENDS})

target_link_libraries(${PROJECT_NAME} PkgConfig::LIBLELY_COAPP
                      PkgConfig::LIBGPIOD phidgets_spatial_parameters)

target_compile_definitions(${PROJECT_NAME}
                           PRIVATE "PANTHER_HARDWARE_INTERFACES_BUILDING_DLL")

pluginlib_export_plugin_description_file(hardware_interface
                                         panther_hardware_interfaces.xml)

install(DIRECTORY include/ DESTINATION include)

install(
  TARGETS ${PROJECT_NAME}
  RUNTIME DESTINATION bin
  ARCHIVE DESTINATION lib
  LIBRARY DESTINATION lib)

install(DIRECTORY config DESTINATION share/${PROJECT_NAME})

if(BUILD_TESTING)
  find_package(ament_cmake_gtest REQUIRED)
  find_package(panther_utils REQUIRED)

  ament_add_gtest(${PROJECT_NAME}_test_panther_imu test/test_panther_imu.cpp)
  target_include_directories(
    ${PROJECT_NAME}_test_panther_imu
    PUBLIC $<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}/test/include> include)
  ament_target_dependencies(${PROJECT_NAME}_test_panther_imu hardware_interface
                            rclcpp panther_utils panther_msgs phidgets_api)
  target_link_libraries(${PROJECT_NAME}_test_panther_imu ${PROJECT_NAME}
                        phidgets_spatial_parameters)

  ament_add_gtest(
    ${PROJECT_NAME}_test_roboteq_error_filter
    test/test_roboteq_error_filter.cpp src/roboteq_error_filter.cpp)
  target_include_directories(${PROJECT_NAME}_test_roboteq_error_filter
                             PRIVATE include)

  ament_add_gtest(${PROJECT_NAME}_test_utils test/test_utils.cpp src/utils.cpp)
  target_include_directories(${PROJECT_NAME}_test_utils PRIVATE include)

  ament_add_gtest(
    ${PROJECT_NAME}_test_roboteq_data_converters
    test/test_roboteq_data_converters.cpp src/roboteq_data_converters.cpp
    src/utils.cpp)
  target_include_directories(
    ${PROJECT_NAME}_test_roboteq_data_converters
    PRIVATE $<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}/test/include> include)
  ament_target_dependencies(${PROJECT_NAME}_test_roboteq_data_converters
                            panther_msgs panther_utils)
  target_link_libraries(${PROJECT_NAME}_test_roboteq_data_converters
                        PkgConfig::LIBLELY_COAPP)

  ament_add_gtest(
    ${PROJECT_NAME}_test_canopen_controller test/test_canopen_controller.cpp
    test/src/roboteqs_mock.cpp src/canopen_controller.cpp
    src/roboteq_driver.cpp src/utils.cpp)
  target_include_directories(
    ${PROJECT_NAME}_test_canopen_controller
    PUBLIC $<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}/test/include> include)
  ament_target_dependencies(${PROJECT_NAME}_test_canopen_controller rclcpp
                            panther_msgs panther_utils)
  target_link_libraries(${PROJECT_NAME}_test_canopen_controller
                        PkgConfig::LIBLELY_COAPP)

  ament_add_gtest(
    ${PROJECT_NAME}_test_roboteq_driver test/test_roboteq_driver.cpp
    test/src/roboteqs_mock.cpp src/canopen_controller.cpp
    src/roboteq_driver.cpp src/utils.cpp)
  target_include_directories(
    ${PROJECT_NAME}_test_roboteq_driver
    PUBLIC $<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}/test/include> include)
  ament_target_dependencies(${PROJECT_NAME}_test_roboteq_driver rclcpp
                            panther_msgs panther_utils)
  target_link_libraries(${PROJECT_NAME}_test_roboteq_driver
                        PkgConfig::LIBLELY_COAPP)

  ament_add_gtest(
    ${PROJECT_NAME}_test_motors_controller
    test/test_motors_controller.cpp
    test/src/roboteqs_mock.cpp
    src/canopen_controller.cpp
    src/roboteq_driver.cpp
    src/roboteq_data_converters.cpp
    src/motors_controller.cpp
    src/utils.cpp)
  target_include_directories(
    ${PROJECT_NAME}_test_motors_controller
    PUBLIC $<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}/test/include> include)
  ament_target_dependencies(${PROJECT_NAME}_test_motors_controller rclcpp
                            panther_msgs panther_utils)
  target_link_libraries(${PROJECT_NAME}_test_motors_controller
                        PkgConfig::LIBLELY_COAPP)

  ament_add_gtest(${PROJECT_NAME}_test_gpiod_driver test/test_gpio_driver.cpp)
  target_include_directories(
    ${PROJECT_NAME}_test_gpiod_driver
    PUBLIC $<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}/include>
           $<INSTALL_INTERFACE:include>)
  ament_target_dependencies(${PROJECT_NAME}_test_gpiod_driver panther_utils)
  target_link_libraries(${PROJECT_NAME}_test_gpiod_driver ${PROJECT_NAME}
                        PkgConfig::LIBGPIOD)

  ament_add_gtest(
    ${PROJECT_NAME}_test_gpiod_controller test/test_gpio_controller.cpp
    src/gpio_controller.cpp src/gpio_driver.cpp)
  target_include_directories(
    ${PROJECT_NAME}_test_gpiod_controller
    PUBLIC $<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}/include>
           $<INSTALL_INTERFACE:include>)
  ament_target_dependencies(${PROJECT_NAME}_test_gpiod_controller panther_utils)
  target_link_libraries(${PROJECT_NAME}_test_gpiod_controller
                        PkgConfig::LIBGPIOD)

  ament_add_gtest(
    ${PROJECT_NAME}_test_panther_system_ros_interface
    test/test_panther_system_ros_interface.cpp
    src/panther_system_ros_interface.cpp
    src/roboteq_data_converters.cpp
    src/utils.cpp
    src/gpio_controller.cpp
    src/gpio_driver.cpp)
  target_include_directories(
    ${PROJECT_NAME}_test_panther_system_ros_interface
    PRIVATE $<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}/test/include> include)
  ament_target_dependencies(
    ${PROJECT_NAME}_test_panther_system_ros_interface
    diagnostic_updater
    rclcpp
    panther_msgs
    panther_utils
    realtime_tools
    std_srvs)
  target_link_libraries(${PROJECT_NAME}_test_panther_system_ros_interface
                        PkgConfig::LIBLELY_COAPP PkgConfig::LIBGPIOD)

  ament_add_gtest(
    ${PROJECT_NAME}_test_panther_system test/test_panther_system.cpp
    test/src/roboteqs_mock.cpp test/src/panther_system_test_utils.cpp)
  set_tests_properties(${PROJECT_NAME}_test_panther_system PROPERTIES TIMEOUT
                                                                      120)
  target_include_directories(
    ${PROJECT_NAME}_test_panther_system
    PUBLIC $<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}/test/include> include)
  ament_target_dependencies(
    ${PROJECT_NAME}_test_panther_system hardware_interface rclcpp panther_msgs
    panther_utils)
  target_link_libraries(${PROJECT_NAME}_test_panther_system
                        PkgConfig::LIBLELY_COAPP)

  install(DIRECTORY test/config DESTINATION share/${PROJECT_NAME}/test)
endif()

ament_export_include_directories(include)

ament_export_libraries(${PROJECT_NAME})

ament_export_dependencies(${PACKAGE_INCLUDE_DEPENDS})

ament_package()
